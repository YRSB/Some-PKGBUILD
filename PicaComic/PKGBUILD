# Maintainer: YRSB <z1knktbkz@mozmail.com>

pkgname=pica-comic
pkgver=4.0.4
pkgrel=1
pkgdesc="A comic app with multiple sources built with flutter."
arch=('x86_64')
url="https://github.com/wgh136/PicaComic"
license=('MIT')
depends=('webkit2gtk-4.1')
makedepends=('flutter')
source=(
  ${pkgname}-${pkgver}.tar.gz::"${url}/archive/refs/tags/v${pkgver}.tar.gz"
  ${pkgname}.desktop
)
sha256sums=(
  'DAC5F6C21E7300E7232583BBA38BCD30058B85AB2D61301B51259585C56D7211'
  '2788b2695e891a58c0a3f4326cb4be49124f3a0cb199eec21cf34c7f036e569d'
)

build() {
  cd ${srcdir}/PicaComic-${pkgver}
  flutter packages get
  flutter build linux --release
}

package() {
  cd ${srcdir}/PicaComic-${pkgver}/build/linux/x64/release/bundle
  find ./data -type f -exec \
                 install -Dm644 {} ${pkgdir}/usr/lib/${pkgname}/{} \;

  install -Dm755 ./lib/* -t ${pkgdir}/usr/lib/${pkgname}/lib/
  install -Dm755 ./pica_comic ${pkgdir}/usr/lib/${pkgname}/pica_comic
  install -Dm644 ${srcdir}/${pkgname}.desktop -t ${pkgdir}/usr/share/applications
  install -Dm644 ./data/flutter_assets/images/app_icon.png ${pkgdir}/usr/share/pixmaps/picacomic.png
  mkdir -p ${pkgdir}/usr/bin
  ln -s /usr/lib/${pkgname}/pica_comic ${pkgdir}/usr/bin/${pkgname}
}
