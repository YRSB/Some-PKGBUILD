# Maintainer: YRSB <z1knktbkz@mozmail.com>

pkgname=pica-comic
pkgver=4.2.1
pkgrel=1
pkgdesc="A comic app with multiple sources built with flutter."
arch=('x86_64')
url="https://github.com/Pacalini/PicaComic"
license=('MIT')
depends=('webkit2gtk-4.1')
makedepends=('fvm')
source=(
  ${pkgname}-${pkgver}.tar.gz::"${url}/archive/refs/tags/v${pkgver}.tar.gz"
  ${pkgname}.desktop
)
sha256sums=(
  '083e0af210501ec63ecd01a7566f336e13ca92b26706b59875d30063b15bdf34'
  '2788b2695e891a58c0a3f4326cb4be49124f3a0cb199eec21cf34c7f036e569d'
)

build() {
  cd ${srcdir}/PicaComic-${pkgver}
  mkdir fvm-cache
  export FVM_CACHE_PATH="./fvm-cache"
  # flutter packages get
  fvm install stable --setup
  fvm use stable
  fvm flutter --no-version-check pub get
  fvm flutter build linux --release
}

package() {
  cd ${srcdir}/PicaComic-${pkgver}/build/linux/x64/release/bundle
  find ./data -type f -exec \
                 install -Dm644 {} ${pkgdir}/usr/lib/${pkgname}/{} \;

  install -Dm755 ./lib/* -t ${pkgdir}/usr/lib/${pkgname}/lib/
  install -Dm755 ./pica_comic ${pkgdir}/usr/lib/${pkgname}/pica_comic
  install -Dm644 ${srcdir}/${pkgname}.desktop -t ${pkgdir}/usr/share/applications
  install -Dm644 ./data/flutter_assets/images/app_icon.png ${pkgdir}/usr/share/pixmaps/picacomic.png
  mkdir -p ${pkgdir}/usr/bin
  ln -s /usr/lib/${pkgname}/pica_comic ${pkgdir}/usr/bin/${pkgname}
}
